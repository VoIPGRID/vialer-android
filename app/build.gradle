apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.voipgrid.app"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 75
        versionName "5.2"
        multiDexEnabled true
        flavorDimensions "default"
        ndk {
            moduleName "pjsua2"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dexOptions {
        // Prevent OutOfMemory with MultiDex during the build phase
        javaMaxHeapSize "4g"
    }
    productFlavors {
        vialer {
            applicationId "com.voipgrid.vialer"
            copy {
                from 'src/vialer/'
                include '*.json'
                into '.'
            }
        }
        acceptatie {
            applicationId "com.voipgrid.vialer"
            copy {
                from 'src/acceptatie/'
                include '*.json'
                into '.'
            }
        }
        voys {
            applicationId "com.voys.app"
            copy {
                from 'src/voys/'
                include '*.json'
                into '.'
            }
        }
        verbonden {
            applicationId "nl.verbonden.app"
            copy {
                from 'src/verbonden/'
                include '*.json'
                into '.'
            }
        }
    }
    lintOptions {
        checkReleaseBuilds true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    jcenter()
    google()
    flatDir {
        dirs 'libs'
    }
}

ext {
    supportLibVersion = "27.0.2"
    espressoVersion = "2.2"
    androidTestVersion = "0.5"
    firebaseVersion = "11.6.2"
    jUnitVersion = "4.12"
    powerMockVersion = "1.7.3"
    okHttpVersion = "3.9.0"
    retroFitVersion = "2.3.0"
    roboElectricVersion = "3.4.2"
    mockitoCoreVersion = '2.13.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Modules.
    compile files('libs/logentries-android.jar')
    compile(name: 'spindle-lock-ring', ext: 'aar')
    compile(name: 'pjsip', ext: 'aar')

    // Support libs.
    compile 'com.android.support:multidex:1.0.2'
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:support-annotations:${supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"

    // Firebase setup.
    compile "com.google.firebase:firebase-messaging:${firebaseVersion}"
    compile "com.google.android.gms:play-services-analytics:${firebaseVersion}"

    // Circle image view
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    // Retrofit and OkHttp
    compile "com.squareup.retrofit2:retrofit:${retroFitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retroFitVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"

    // Required -- JUnit 4 framework.
    testCompile "junit:junit:${jUnitVersion}"

    // Testing below are supported api level because SNAPSHOT version is to unstable.
    testCompile "org.robolectric:robolectric:${roboElectricVersion}"
    testCompile "org.mockito:mockito-core:${mockitoCoreVersion}"

    // Setup for local tests.
    testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testCompile "org.powermock:powermock-module-junit4-rule:${powerMockVersion}"
    testCompile "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    testCompile "org.powermock:powermock-classloading-xstream:${powerMockVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"

    // Setup for instrumented tests.
    androidTestCompile "com.android.support.test:runner:${androidTestVersion}"
    androidTestCompile "com.android.support.test:rules:${androidTestVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
}

// Needs to be applied at the bottom of the gradle file otherwise it will create a conflict.
apply plugin: "com.google.gms.google-services"
