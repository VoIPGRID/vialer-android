apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.voipgrid.app"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 59
        versionName "4.2 beta"
        multiDexEnabled true
        ndk {
            moduleName "pjsua2"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dexOptions {
        // Prevent OutOfMemory with MultiDex during the build phase
        javaMaxHeapSize "4g"
    }
    productFlavors {
        vialer {
            applicationId "com.voipgrid.vialer"
            copy {
                from 'src/vialer/'
                include '*.json'
                into '.'
            }
        }
        voys {
            applicationId "com.voys.app"
            copy {
                from 'src/voys/'
                include '*.json'
                into '.'
            }
        }
        verbonden {
            applicationId "nl.verbonden.app"
            copy {
                from 'src/verbonden/'
                include '*.json'
                into '.'
            }
        }
    }
    lintOptions {
        checkReleaseBuilds true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    flatDir {
        dirs 'libs'
    }
}

ext {
    supportLibVersion = "25.2.0"
    espressoVersion = "2.2"
    androidTestVersion = "0.5"
    firebaseVersion = "10.2.0"
    jUnitVersion = "4.12"
    powerMockVersion = "1.6.4"
    okHttpVersion = "3.2.0"
    retroFitVersion = "2.0.0-beta4"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Modules.
    compile project(':pjsip')
    compile files('libs/logentries-android.jar')
    compile(name: 'spindle-lock-ring', ext: 'aar')

    // Support libs.
    compile "com.android.support:multidex:1.0.1"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:support-annotations:${supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"

    // Firebase setup.
    compile "com.google.firebase:firebase-messaging:${firebaseVersion}"
    compile "com.google.android.gms:play-services-analytics:${firebaseVersion}"

    // Circle image view
    compile "de.hdodenhof:circleimageview:2.0.0"
    compile "com.amulyakhare:com.amulyakhare.textdrawable:1.0.1"

    // Retrofit and OkHttp
    compile "com.squareup.retrofit2:retrofit:${retroFitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retroFitVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"

    // Required -- JUnit 4 framework.
    testCompile "junit:junit:${jUnitVersion}"

    // Testing below are supported api level because SNAPSHOT version is to unstable.
    testCompile "org.robolectric:robolectric:3.0"
    testCompile "org.mockito:mockito-core:1.10.19"

    // Setup for local tests.
    testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testCompile "org.powermock:powermock-module-junit4-rule:${powerMockVersion}"
    testCompile "org.powermock:powermock-api-mockito:${powerMockVersion}"
    testCompile "org.powermock:powermock-classloading-xstream:${powerMockVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"

    // Setup for instrumented tests.
    androidTestCompile "com.android.support.test:runner:${androidTestVersion}"
    androidTestCompile "com.android.support.test:rules:${androidTestVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
}

// Needs to be applied at the bottom of the gradle file otherwise it will create a conflict.
apply plugin: "com.google.gms.google-services"
