apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.voipgrid.app"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 220
        versionName "6.6 alpha 16"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        ndk {
            moduleName "pjsua2"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        vialer {
            keyAlias keystoreProperties['vialerKeyAlias']
            keyPassword keystoreProperties['vialerKeyPassword']
            storeFile file(keystoreProperties['vialerStoreFile'])
            storePassword keystoreProperties['vialerStorePassword']
        }
        voys {
            keyAlias keystoreProperties['voysKeyAlias']
            keyPassword keystoreProperties['voysKeyPassword']
            storeFile file(keystoreProperties['voysStoreFile'])
            storePassword keystoreProperties['voysStorePassword']
        }
        verbonden {
            keyAlias keystoreProperties['verbondenKeyAlias']
            keyPassword keystoreProperties['verbondenKeyPassword']
            storeFile file(keystoreProperties['verbondenStoreFile'])
            storePassword keystoreProperties['verbondenStorePassword']
        }
        annabel {
            keyAlias keystoreProperties['annabelKeyAlias']
            keyPassword keystoreProperties['annabelKeyPassword']
            storeFile file(keystoreProperties['annabelStoreFile'])
            storePassword keystoreProperties['annabelStorePassword']
        }
    }
    dexOptions {
        // Prevent OutOfMemory with MultiDex during the build phase
        javaMaxHeapSize "4g"
    }
    flavorDimensions "default"
    productFlavors {
        vialer {
            applicationId "com.voipgrid.vialer"
            dimension = 'default'
            copy {
                from 'src/vialer/'
                include '*.json'
                into '.'
            }
        }
        staging {
            applicationId "com.voipgrid.vialer"
            dimension = 'default'
            copy {
                from 'src/staging/'
                include '*.json'
                into '.'
            }
            copy {
                from 'src/vialer/'
                include 'google-services.json'
                into '.'
            }
        }
        voys {
            applicationId "com.voys.app"
            dimension = 'default'
            copy {
                from 'src/voys/'
                include '*.json'
                into '.'
            }
        }
        verbonden {
            applicationId "nl.verbonden.app"
            dimension = 'default'
            copy {
                from 'src/verbonden/'
                include '*.json'
                into '.'
            }
        }
        annabel {
            applicationId "com.bellenmetannabel.app"
            dimension = 'default'
            copy {
                from 'src/annabel/'
                include '*.json'
                into '.'
            }
        }
    }
    lintOptions {
        checkReleaseBuilds true
    }
    buildTypes {
        release {
            productFlavors.vialer.signingConfig signingConfigs.vialer
            productFlavors.voys.signingConfig signingConfigs.voys
            productFlavors.verbonden.signingConfig signingConfigs.verbonden
            productFlavors.annabel.signingConfig signingConfigs.annabel

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true"]
        }
        debug {
            manifestPlaceholders = [enableCrashReporting: "false"]
            buildConfigField("String", "TEST_USERNAME", "\"${project.properties['testUsername'] ?: ""}\"")
            buildConfigField("String", "TEST_PASSWORD", "\"${project.properties['testPassword'] ?: ""}\"")
            buildConfigField("String", "TEST_END_NUMBER", "\"${project.properties['testEndNumber'] ?: ""}\"")
            versionNameSuffix '-DEBUG'
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    google()
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

ext {
    supportLibVersion = "1.0.2"
    espressoVersion = "2.2"
    androidTestVersion = '1.2.0'
    firebaseCoreVersion = '17.2.0'
    firebaseMessagingVersion = '20.1.0'
    okHttpVersion = '4.1.1'
    retroFitVersion = '2.6.1'
    roboElectricVersion = '4.3'
    mockitoCoreVersion = "2.23.0"
    butterKnifeVersion = '10.1.0'
    daggerVersion = '2.24'
    workVersion = '2.2.0'
    kotPrefVersion = "2.8.0"
    room_version = '2.2.0-beta01'
    lifecycle_version = "1.1.1"
    koin_version = "2.0.1"
    nav_version = "2.3.0-alpha02"
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Modules.
    implementation files('libs/logentries-android.jar')
    implementation(name: 'pjsip', ext: 'aar')

    implementation "androidx.appcompat:appcompat:${supportLibVersion}"
    implementation 'com.google.android.material:material:1.1.0-alpha10'
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc01"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0-rc01"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Firebase setup.
    implementation "com.google.firebase:firebase-core:${firebaseCoreVersion}"
    implementation "com.google.firebase:firebase-messaging:${firebaseMessagingVersion}"

    // Firebase Performance Measurement.
    implementation 'com.google.firebase:firebase-perf:19.0.0'

    // Circular image view
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    // Retrofit and OkHttp - Api calls
    implementation "com.squareup.retrofit2:retrofit:${retroFitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retroFitVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"

    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'

    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    kapt "com.google.dagger:dagger-android-processor:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    implementation 'com.github.tamir7.contacts:contacts:1.1.7'
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.3.0"

    implementation 'javax.inject:javax.inject:1@jar'
    implementation 'com.github.lzyzsd.randomcolor:library:1.0.0'

    implementation 'com.github.f0ris.sweetalert:library:1.6.2'
    // Butter knife - Field and method binding for views
    implementation "com.jakewharton:butterknife:${butterKnifeVersion}"
    kapt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    // App rating popup
    implementation 'com.codemybrainsout.rating:ratingdialog:1.0.8'

    // Scytale -  Manage key generation
    implementation 'com.yakivmospan:scytale:1.0.1'
    implementation "com.github.nisrulz:easydeviceinfo-base:2.4.1"
    implementation 'joda-time:joda-time:2.10.1'
    implementation "android.arch.paging:runtime:1.0.1"

    // Testing below are supported api level because SNAPSHOT version is to unstable.
    testImplementation "org.robolectric:robolectric:${roboElectricVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
    testImplementation 'androidx.test.ext:junit:1.1.1'

    // Setup for instrumented tests.
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
    androidTestImplementation "androidx.test:rules:${androidTestVersion}"
    implementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'

    // Automated screenshots for instrumented tests.
    androidTestImplementation 'tools.fastlane:screengrab:1.1.0'

    // Crashlytics
    releaseImplementation "com.crashlytics.sdk.android:crashlytics:2.10.1"
    implementation "androidx.core:core-ktx:1.0.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.work:work-runtime-ktx:$workVersion"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation "androidx.fragment:fragment-ktx:1.2.0"

    implementation "com.chibatching.kotpref:kotpref:$kotPrefVersion"
    implementation "com.chibatching.kotpref:initializer:$kotPrefVersion"
    implementation "com.chibatching.kotpref:enum-support:$kotPrefVersion"
    implementation "com.chibatching.kotpref:gson-support:$kotPrefVersion"
    implementation "androidx.paging:paging-runtime-ktx:2.1.0"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    // Segment analytics
    implementation 'com.segment.analytics.android:analytics:4.3.1'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    implementation 'com.googlecode.libphonenumber:libphonenumber:8.8.3'
    implementation 'com.chaos.view:pinview:1.4.3'
    implementation 'com.github.myinnos:AlphabetIndex-Fast-Scroll-RecyclerView:1.0.95'
    implementation 'com.kassylab:recyclerview-cursor-adapter:26.1.3'
}

// Needs to be applied at the bottom of the gradle file otherwise it will create a conflict.
apply plugin: "com.google.gms.google-services"
